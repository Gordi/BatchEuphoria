apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin:'application'

group = "de.dkfz.roddy"

mainClassName = 'de.dkfz.roddy.BEIntegrationTestStarter'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'


/*
 * Gets the version name from the current Git tag. If the current commit is not tagged,
 * this returned string will indicate that. Also if the repository is dirty.
 */
def getVersionName() {
    String[] command = ["/usr/bin/env", "git", "-C", "${project.rootDir}", "describe", "--tags", "--dirty"]
    def process = new ProcessBuilder(command).redirectErrorStream(true).start()
    def output = process.inputStream.readLines().join("")
    if (process.waitFor() != 0) {
        throw new RuntimeException("Command failed with code ${process.exitValue()}: '${command.join(" ")}'\nError output: '${output}'")
    } else {
        return output.trim()
    }
}


repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    testCompile 'junit:junit:4.12'
    compile 'net.logstash.logback:logstash-logback-encoder:4.7'
    compile group: 'net.schmizz', name: 'sshj', version: '0.10.0'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.9'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.3'
    compile group: 'commons-cli', name: 'commons-cli', version: '1.2'
    compile 'com.github.eilslabs:RoddyToolLib:0.0.1'
}

task listJars {
    doLast {
        configurations.compile.each { File file -> println file.name }
    }
}


task uberjar(type: Jar,dependsOn:[':compileJava',':compileGroovy']) {
    from files(sourceSets.main.output.classesDir)
    from configurations.runtime.asFileTree.files.collect { zipTree(it) }

    version = getVersionName()

    manifest {
        attributes 'Main-Class': mainClassName
        attributes 'Implementation-Version': version
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}
